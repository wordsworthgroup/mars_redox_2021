module thermodynamics

  use composition, only : R_H2O
  
  !--------------------------------------------------------
  !  
  ! Key thermodynamical relations needed to compute
  ! atmospheric structure and heating rates.
  !
  ! Robin Wordsworth (2016)
  ! some subroutines from NBS/NRC Steam Tables.
  !
  !--------------------------------------------------------

  implicit none
  private

  public :: cp_H2O, cp_CO2, get_psat_H2O, get_Tsat_H2O, get_Tsat_CO2
  public :: get_Tsat_H2O_CC
  public :: get_Tsat_H2O_GB
  public :: tdpsdt, therm

contains
  
  ! ==================================================================================

  real(8) function cp_H2O(T)
    ! specific heat capacity at constant pressure [kJ/kg/K]
    ! created from http://www.engineeringtoolbox.com/water-vapor-d_979.html
    ! data using matlab script poly_cp.m
    ! checked vs. CRC Handbook ed. 96 24/5/16  
    implicit none

    real(8), intent(in) :: T ! temperature [K]

    real(8) logT
    real(8), parameter :: P(4) = [-0.0878, 1.9262, -13.3665, 31.6932]

    logT = log(T)
    
    cp_H2O = P(1)*logT**3 + P(2)*logT**2 + P(3)*logT + P(4)

    return
  end function cp_H2O

  ! ==================================================================================

  real(8) function cp_CO2(T)
    ! specific heat capacity at constant pressure [kJ/kg/K]
    ! created from http://www.engineeringtoolbox.com/carbon-dioxide-d_974.html
    ! data using matlab script poly_cp.m
    
    implicit none

    real(8), intent(in) :: T ! temperature [K]
    real(8) logT
    real(8), parameter :: P(3) = [-0.0542, 0.9851, -2.9930]

    logT = log(T)
    
    cp_CO2 = P(1)*logT**2 + P(2)*logT + P(3)

    return
  end function cp_CO2

  ! ==================================================================================

  subroutine get_Tsat_H2O(p,Tsat)

    ! calculate saturation vapor temperature of H2O
    ! based on a polynomial fit to the curve generated by get_psat_H2O
    ! good between 180 and 500 K at least
    
    implicit none

    real(8), intent(in)  :: p    ! pressure [Pa]
    real(8), intent(out) :: Tsat ! saturation temperature [K]

    ! logarithm of pressure
    real(8) logp

    ! polynomial coefficients
    real(8), parameter :: A(5) = [0.0046, -0.0507, 0.5631, 7.5861, 207.2399]
    
    logp = log(p)
    Tsat = A(1)*logp**4 + A(2)*logp**3 + A(3)*logp**2 + A(4)*logp + A(5) 
    
    return
  end subroutine get_Tsat_H2O

  ! ==================================================================================

  subroutine get_Tsat_H2O_GB(p,Tsat)

    ! calculate saturation vapor temperature of H2O
    ! based on the curve used in Goldblatt+2013
    
    implicit none

    real(8), intent(in)  :: p    ! pressure [Pa]
    real(8), intent(out) :: Tsat ! saturation temperature [K]

    ! logarithm of pressure
    real(8) logp

    ! polynomial coefficients
    real(8), parameter :: A(5) = [2.5653e-05,  -2.7667e-04,    0.0016,    0.0334,    5.3595]
    
    logp = log(p)
    Tsat = exp(A(1)*logp**4 + A(2)*logp**3 + A(3)*logp**2 + A(4)*logp + A(5))
       
    return
  end subroutine get_Tsat_H2O_GB

  ! ==================================================================================

  subroutine get_Tsat_H2O_CC(p,Tsat)

    use fund_consts, only : Rstar
    
    implicit none

    ! to be improved (better not to hard-code this variables)
    real(8), parameter :: mu_H2O  = 18.01528     ! molar mass of H2O [g/mol]
    real(8), parameter :: T0      = 273.16       ! triple point temperature [K]
    real(8), parameter :: p0      = 610.78       ! triple point pressure [Pa]
    real(8), parameter :: hlv     = 2.255e6      ! latent heat of evaporation [J/kg]
    real(8), parameter :: rvgas   = Rstar/(mu_H2O/1.0d3) ! specific gas constant [J/kg/K]

    real(8), intent(in)  :: p                 ! pressure [Pa]
    real(8), intent(out) :: Tsat              ! condensation temperature [K]

    Tsat = T0/(1.0d0 - (rvgas*T0/hlv)*log(p/p0))
    
    return
  end subroutine get_Tsat_H2O_CC

  ! ==================================================================================

  !subroutine get_psat_H2O_CC(T,psat)

  !  ! calculate saturation vapor pressure of H2O using Clausius-Clayperon

  !  use composition, only : rvgas, hlv, T0, p0

  !  implicit none

  !  real(8), intent(in)  :: T    ! temperature [K]
  !  real(8), intent(out) :: psat ! saturation pressure [Pa]

  !  psat = p0*exp(-(hlv/rvgas)*(1.0d0/T - 1.0d0/T0))
  !  
  !  return
  !end subroutine get_psat_H2O_CC

  ! ==================================================================================

  subroutine get_psat_H2O(T,psat)

    ! calculate saturation vapor pressure of H2O using
    ! expressions from Murphy & Koop (2005), QJRMS

    implicit none
    
    real(8), intent(in)  :: T     ! temperature [K]
    real(8), intent(out) :: psat ! saturation pressure [Pa]

    if(T>273.15d0)then
       psat = exp(54.842763 - 6763.22/T - 4.210*log(T) + 0.000367*T &
            + tanh(0.0415*(T - 218.8))*(53.878 - 1331.22/T &
            - 9.44523*log(T) + 0.014025*T))
    else
       psat = exp(9.550426 - 5723.265/T + 3.53068*log(T) - 0.00728332*T)
    end if
    
  end subroutine get_psat_H2O
    
  ! ==================================================================================

  subroutine get_psat_H2O_ST (T, psat)
  !subroutine get_psat_H2O_ST (T, psat)
    !
    !! PSAT_H2O makes a rough estimate of the vapor pressure.
    !
    !  Discussion:
    !
    !    The calculation agrees with tabulated data to within
    !    0.02% for temperature to within a degree or so of the critical
    !    temperature.  The approximate vapor pressure can be refined
    !    by imposing the condition that the Gibbs functions of the vapor
    !    and liquid phases be equal.
    !
    !  Modified:
    !
    !    21 November 1998
    !
    !  Reference:
    !
    !    Lester Haar, John Gallagher and George Kell,
    !    NBS/NRC Steam Tables:
    !    Thermodynamic and Transport Properties and Computer Programs
    !      for Vapor and Liquid States of Water in SI Units,
    !    Hemisphere Publishing Corporation, Washington, 1984,
    !    TJ270.H3
    !
    !    C A Meyer, R B McClintock, G J Silvestri, R C Spencer,
    !    ASME Steam Tables: Thermodynamic and Transport Properties of Steam,
    !    American Society of Mechanical Engineers, 1967.
    !
    !  Parameters:
    !
    !    Input, real(8) T, the temperature, in degrees Kelvin.
    !
    !    Output, real(8) Psat, the vapor pressure, in Pascals.
    
    implicit none

    real(8), intent(in) :: t
    real(8), intent(out) :: psat
    
    integer i
    real(8), parameter, dimension ( 8 ) :: a = (/ &
         -7.8889166D+00,   2.5514255D+00,   -6.716169D+00, 33.239495D+00, &
         -105.38479D+00,   174.35319D+00,  -148.39348D+00, 48.631602D+00 /)
    real(8) b
    real(8) q
    real(8) p
    real(8), parameter :: t_ref = 647.25D+00
    real(8) v
    real(8) w
    real(8) z


    
    !
    !  Refuse to handle zero or negative temperatures.
    !
    if ( t <= 0.0D+00 ) then
       write ( *, '(a)' ) ' '
       write ( *, '(a)' ) 'PSAT_H2O - Fatal error!'
       write ( *, '(a)' ) '  The input temperature T must be positive.'
       write ( *, '(a,g14.6)' ) '  Input value was T = ', t
       stop
    end if

    if ( t <= 314.0D+00 ) then

       p = 0.1D+00 * exp ( 6.3573118D+00 - 8858.843D+00 / t &
            + 607.56335D+00 * t**( -0.6D+00 ) )

    else

       v = t / t_ref
       w = abs ( 1.0D+00 - v )
       b = 0.0D+00
       do i = 1, 8
          z = i
          b = b + a(i) * w**( ( z + 1.0D+00 ) / 2.0D+00 )
       end do

       q = b / v
       p = 22.093D+00 * exp ( q )

    end if

    ! convert megaPa to Pa
    psat = p*1.0d6

    return
  end subroutine get_psat_H2O_ST
  
  ! ==================================================================================

  subroutine get_Tsat_CO2(p,tcond)
    ! Calculates the condensation temperature for CO2

    implicit none

    real(8), intent(in)  :: p                  ! pressure [Pa]
    real(8), intent(out) :: tcond              ! condensation temperature [K]
    real(8), parameter   :: ptriple = 518000.0

    if(p<ptriple)then
       tcond = -3167.8d0/(log(1.0d-2*p)-23.23d0)
       ! Fanale+ 1982 
    else
       tcond = 684.2d0 - 92.3d0*log(p) + 4.32d0*log(p)**2
       ! liquid-vapour transition (based on CRC handbook 2003 data)
    endif

    return
  end subroutine get_Tsat_CO2

  ! ==================================================================================

  ! Extra routines required for atm_profile.f90:

  ! ==================================================================================


  ! ---------------------
  ! H2O
  ! ---------------------

  ! ==================================================================================
  subroutine base ( t, rho, ab, cvb, dpdrb, dpdtb, gb, hb, pb, sb, ub )
    !
    !*******************************************************************************
    !
    !! BASE calculates quantities associated with the base Helmholtz function.
    !
    !
    !  Discussion:
    !
    !    The equation for the base Helmholtz function AB(T,RHO) is:
    !
    !      AB(T,RHO) = R * T * (
    !        - ln ( 1 - y ) 
    !        - ( beta - 1 ) / ( 1 - y ) 
    !        + ( alpha + beta + 1 ) / ( 2 * ( 1 - y )**2 )
    !        + 4 * y * ( ( Bbar / b ) - gamma ) 
    !        - 0.5 * ( alpha - beta + 3 ) 
    !        + ln ( RHO * R * T / P0 ) )
    !                                                      (Equation 2)
    !   where 
    !
    !     y = b * rho / 4, 
    !     alpha = 11,
    !     beta = 133/3, 
    !     gamma = 7/2, 
    !     P0 = 0.101325 MegaPascals = 1 atm
    !
    !   and
    !
    !     b(T) = b1 * ln(T/T0) + sum(j=0,1,3,5) b(j)*(T0/T)**j  (Equation 3)
    !
    !     Bbar(T) = sum(j=0,1,2,4) B(j)*(T0/T)**j               (Equation 4).
    !
    !   where 
    !
    !     T0=647.073 K and the coefficients b(j) and B(j) are
    !    
    !     j    b(j)                         B(j)
    !    --    -----------                  ----------
    !     0    0.7478629                    1.1278334
    !     1   -0.3540782                   -0.5944001
    !     2    0                           -5.010996
    !     3    0.007159876                  0
    !     4    0                            0.63684256
    !     5   -0.003528426                  0
    !
    !  For the derived quantities, the following relations are used:
    !
    !    Pressure:                  PB      = RHO**2 * dAB/dRHO
    !    Density derivative:        DPDRB   = 2*PB/RHO + RHO**2 * d2AB/dRHO2
    !    Temperature derivative:    DPDTB   = RHO**2 * d2AB/(dRHO dT)
    !    Specific entropy:          SB      = ( UB - AB ) / T
    !    Specific internal energy:  UB      = AB + T * SB
    !    Specific enthalpy:         HB      = UB + PB / RHO
    !    Specific heat capacity
    !      at constant volume:      CVB     = - T * d2AB/dT2
    !    Specific Gibbs function:   GB      = AB + PB / RHO
    !
    !
    !  Reference:
    !
    !    Lester Haar, John Gallagher and George Kell,
    !    NBS/NRC Steam Tables:
    !    Thermodynamic and Transport Properties and Computer Programs
    !      for Vapor and Liquid States of Water in SI Units,
    !    Hemisphere Publishing Corporation, Washington, 1984,
    !    TJ270.H3
    !
    !    C A Meyer, R B McClintock, G J Silvestri, R C Spencer,
    !    ASME Steam Tables: Thermodynamic and Transport Properties of Steam,
    !    American Society of Mechanical Engineers, 1967.
    !
    !  Modified:
    !
    !    03 February 2002
    !
    !  Parameters:
    !
    !    Input, real(8) T, the temperature, in degrees Kelvin.
    !
    !    Input, real(8) RHO, the density, in G/CM3.
    !
    !    Output, real(8) AB, the base value of the Helmholtz function,
    !    in KJ/kg.
    !
    !    Output, real(8) CVB, the base value of the isochoric (constant 
    !    volume) heat capacity, in KJ/(kg degrees Kelvin).
    !
    !    Output, real(8) DPDRB, the base value of the partial
    !    derivative dP(T,RHO)/dRHO, with T held fixed, in (MegaPascals CM3)/G.
    !
    !    Output, real(8) DPDTB, the base value of the partial
    !    derivative dP(T,RHO)/dT, with RHO held fixed, in 
    !    MegaPascals/degrees Kelvin.
    !
    !    Output, real(8) GB, the base value of the Gibbs free energy,
    !    in KJ/kg.
    !
    !    Output, real(8) HB, the base value of enthalpy, in KJ/kg.
    !
    !    Output, real(8) PB, the base pressure, in MegaPascals.
    !
    !    Output, real(8) SB, the base value of entropy, 
    !    in KJ/(kg degrees Kelvin).
    !
    !    Output, real(8) UB, the base value of internal energy, 
    !    in KJ/kg.
    !
    implicit none
    !
    real(8) ab
    real(8), parameter :: alpha = 11.0D+00
    real(8) b1
    real(8) b1t
    real(8) b1tt
    real(8) b2
    real(8) b2t
    real(8) b2tt
    real(8), parameter :: beta = 44.333333333333D+00
    real(8) cvb
    real(8) dpdrb
    real(8) dpdtb
    real(8) dz
    real(8) dz0
    real(8), parameter :: gamma = 3.5D+00
!    real(8) gascon
    real(8) gb
    real(8) hb
    real(8), parameter :: p_zero = 0.101325D+00
    real(8) pb
    real(8) rho
    real(8) sb
    real(8) t
    real(8) ub
    real(8) x
    real(8) y
    real(8) z
    real(8) z0
    !
    !  Refuse to handle zero or negative temperatures.
    !
    if ( t <= 0.0 ) then
       write ( *, '(a)' ) ' '
       write ( *, '(a)' ) 'BASE - Fatal error!'
       write ( *, '(a)' ) '  The input temperature T must be positive.'
       write ( *, '(a,g14.6)' ) '  Input value was T = ', t
       stop
    end if
    !
    !  Refuse to handle zero or negative density.
    !
    if ( rho <= 0.0D+00 ) then
       write ( *, '(a)' ) ' '
       write ( *, '(a)' ) 'BASE - Fatal error!'
       write ( *, '(a)' ) '  The input density RHO must be positive.'
       write ( *, '(a,g14.6)' ) '  Input value was RHO = ', rho
       stop
    end if
    !
    !  Compute auxilliary quantities for Equation 2.
    !
    call bb ( t, b1, b2, b1t, b2t, b1tt, b2tt )

    y = 0.25D+00 * b1 * rho

    x = 1.0D+00 - y
    !
    !  Evaluate Equation 2.
    !
    ab =   - log ( 1.0D+00 - y ) &
         - ( beta - 1.0D+00 ) / ( 1.0D+00 - y ) &
         + ( alpha + beta + 1.0D+00 ) / ( 2.0D+00 * ( 1.0D+00 - y )**2 ) &
         + 4.0D+00 * y * ( ( b2 / b1 ) - gamma ) &
         - 0.5D+00 * ( alpha - beta + 3.0D+00 ) &
         + log ( rho * R_h2O * t / p_zero )
    !
    !  Determine quantities defined in terms of AB.
    !
    pb = ( 1.0D+00 + alpha * y + beta * y**2 ) / ( 1.0D+00 - y )**3 &
         + 4.0D+00 * y * ( b2 / b1 - gamma )

    z0 = ( 1.0D+00 + alpha * y + beta * y**2 ) / ( 1.0D+00 - y )**3

    z = z0 + 4.0D+00 * y * ( b2 / b1 - gamma )

    dz0 = ( alpha + 2.0D+00 * beta * y ) / ( 1.0D+00 - y )**3 &
         + 3.0D+00 * ( 1.0D+00 + alpha * y + beta * y**2 ) / ( 1.0D+00 - y )**4

    dz = dz0 + 4.0D+00 * ( b2 / b1 - gamma )

    gb = ab + pb

    ub = - t * b1t * ( pb - 1.0D+00 - rho * b2 ) / b1 - rho * t * b2t

    hb = pb + ub
    !
    !  An incorrect version of this equation began:
    !
    !    cvb = 2.0D+00 * ub + ( pb - 1.0D+00 ) &
    !
    !  and caused me no end of trouble.  My fault, JVB, 03 February 2002
    !
    cvb = 2.0D+00 * ub + ( z0 - 1.0D+00 ) &
         * ( ( t * b1t / b1 )**2 - t**2 * b1tt / b1 ) &
         - rho * t**2 * ( b2tt - gamma * b1tt ) - ( t * b1t / b1 )**2 * y * dz0

    dpdtb = pb / t + rho * ( 0.25D+00 * ( dz0 + 4.0D+00 * ( b2 / b1 - gamma ) ) &
         * b1t + b2t - b2 / b1 * b1t )

    sb = ub - ab

    dpdrb = pb + y * ( dz0 + 4.0D+00 * ( b2 / b1 - gamma ) )
    !
    !  Assign dimensions.
    !
    ab =    R_H2O * t       * ab
    cvb =   R_H2O           * cvb
    dpdrb = R_H2O * t       * dpdrb
    dpdtb = R_H2O * t * rho * dpdtb
    gb =    R_H2O * t       * gb
    hb =    R_H2O * t       * hb
    pb =    R_H2O * t * rho * pb
    sb =    R_H2O           * sb
    ub =    R_H2O * t       * ub

    return
  end subroutine base

  ! ==================================================================================
  subroutine bb ( t, b1, b2, b1t, b2t, b1tt, b2tt )
    !
    !*******************************************************************************
    !
    !! BB calculates the B's of equations 3 and 4.  
    !
    !
    !  Discussion:
    !
    !    Here
    !
    !      b(T) = b1 * ln(T/T0) + sum(j=0,1,3,5) b(j)*(T0/T)**j  (Equation 3)
    !
    !      Bbar(T) = sum(j=0,1,2,4) B(j)*(T0/T)**j               (Equation 4).
    !
    !    where 
    !
    !      T0 = 647.073 K 
    !
    !    and the coefficients b(j) and B(j) are
    !    
    !      j    b(j)                         B(j)
    !     --    -----------                  ----------
    !      0    0.7478629                    1.1278334
    !      1   -0.3540782                   -0.5944001
    !      2    0                           -5.010996
    !      3    0.007159876                  0
    !      4    0                            0.63684256
    !      5   -0.003528426                  0
    !
    !
    !  Reference:
    !
    !    Lester Haar, John Gallagher and George Kell,
    !    NBS/NRC Steam Tables:
    !    Thermodynamic and Transport Properties and Computer Programs
    !      for Vapor and Liquid States of Water in SI Units,
    !    Hemisphere Publishing Corporation, Washington, 1984,
    !    TJ270.H3
    !
    !    C A Meyer, R B McClintock, G J Silvestri, R C Spencer,
    !    ASME Steam Tables: Thermodynamic and Transport Properties of Steam,
    !    American Society of Mechanical Engineers, 1967.
    !
    !  Parameters:
    !
    !    Input, real(8) T, the temperature, in degrees Kelvin.
    !
    !    Output, real(8) B1, the coefficient b from equation 3, 
    !    in CM3/G.
    !
    !    Output, real(8) B2, the coefficient Bbar from equation 4, 
    !    in CM3/G.
    !
    !    Output, real(8) B1T, the derivative dB1/dT, 
    !    in (CM3)/(G Degrees Kelvin).
    !
    !    Output, real(8) B2T, the derivative dB2/dT, 
    !    in (CM3)/(G Degrees Kelvin).
    !
    !    Output, real(8) B1TT, the second derivative of B1 with 
    !    respect to T, in (CM3)/(G (Degrees Kelvin)**2 ).
    !
    !    Output, real(8) B2TT, the second derivative of B2 with 
    !    respect to T, in (CM3)/(G (Degrees Kelvin)**2 ).
    !
    implicit none
    !
    real(8) b1
    real(8) b1t
    real(8) b1tt
    real(8) b2
    real(8) b2t
    real(8) b2tt
    real(8), parameter, dimension ( 10 ) :: bp = (/ &
         0.7478629D+00,   -0.3540782D+00,    0.0D+00,           0.0D+00, &
         0.007159876D+00,  0.0D+00,         -0.003528426D+00,   0.0D+00, &
         0.0D+00,          0.0D+00 /)
    real(8), parameter, dimension ( 10 ) :: bq = (/ &
         1.1278334D+00,    0.0D+00,         -0.5944001D+00,   -5.010996D+00, &
         0.0D+00,          0.63684256D+00,   0.0D+00,          0.0D+00, &
         0.0D+00,          0.0D+00 /)
    integer i
    real(8) t
    real(8), parameter :: t_ref = 647.073D+00
    real(8) v(10)
    !
    !  Refuse to handle zero or negative temperatures.
    !
    if ( t <= 0.0D+00 ) then
       write ( *, '(a)' ) ' '
       write ( *, '(a)' ) 'BB - Fatal error!'
       write ( *, '(a)' ) '  The input temperature T must be positive.'
       write ( *, '(a,g14.6)' ) '  Input value was T = ', t
       stop
    end if
    !
    !  Set V(I) = ( T_REF / T )**(I-1).
    !
    v(1) = 1.0D+00
    do i = 2, 10
       v(i) = v(i-1) * t_ref / t
    end do
    !
    !  Set B1, B1T, B1TT.
    !
    b1 = bp(1) + bp(2) * log ( 1.0D+00 / v(2) )
    b1t = bp(2) * v(2) / t_ref
    b1tt = 0.0D+00
    do i = 3, 10
       b1 = b1 + bp(i) * v(i-1)
       b1t = b1t - dble ( i - 2 ) * bp(i) * v(i-1) / t
       b1tt = b1tt + bp(i) * dble ( i - 2 )**2 * v(i-1) / t**2
    end do

    b1tt = b1tt -  ( b1t / t )
    !
    !  Set B2, B2T, B2TT.
    !
    b2 = bq(1)
    b2t = 0.0D+00
    b2tt = 0.0D+00
    do i = 3, 10
       b2 = b2 + bq(i) * v(i-1)
       b2t = b2t - dble ( i - 2 ) * bq(i) * v(i-1) / t
       b2tt = b2tt + bq(i) * dble ( i - 2 )**2 * v(i-1) / t**2
    end do

    b2tt = b2tt - ( b2t / t )

    return
  end subroutine bb

  ! ==================================================================================

  subroutine ideal ( t, ai, cpi, cvi, gi, hi, si, ui )
    !
    !*******************************************************************************
    !
    !! IDEAL computes ideal gas thermodynamic properties of water.
    !
    !
    !  Discussion:
    !
    !    Values for thermodynamic properties of water in the ideal
    !    gas state were reported by Woolley.  The formula for the ideal gas
    !    term of the Helmholtz function approximates a term by term summation of 
    !    contributions from each of the rotation and vibration states.  
    !    The formula, equation #6 in the reference, is:
    !   
    !    A(ideal)(T) = -R * T * ( 1 + ( C(1)/Tr + C(2) ) * ln(Tr)
    !      + Sum ( 3 <= I <= 18) C(I) * Tr**(I-6)
    !   
    !    where Tr=T/100 K.  The C(i) are tabulated coefficients.  Equation
    !    6 can be used for temperatures below 3000 K, and is accurate to
    !    within the tolerance of the gas constant for 50<=T<=2000 K.
    !     
    !  Reference:
    !
    !    Lester Haar, John Gallagher and George Kell,
    !    NBS/NRC Steam Tables:
    !    Thermodynamic and Transport Properties and Computer Programs
    !      for Vapor and Liquid States of Water in SI Units,
    !    Hemisphere Publishing Corporation, Washington, 1984,
    !    TJ270.H3
    !
    !  Parameters:
    !
    !    Input, real(8) T, the temperature, in degrees Kelvin.
    !
    !    Output, real(8) AI, the Helmholtz function, in KJ/kg.
    !
    !    Output, real(8) CPI, the heat capacity at constant pressure,
    !    in KJ/(kg degrees Kelvin).
    !
    !    Output, real(8) CVI, the heat capacity at constant volume,
    !    in KJ/(kg degrees Kelvin).
    !
    !    Output, real(8) GI, the Gibbs free energy, in KJ/kg.
    !
    !    Output, real(8) HI, the enthalpy, in KJ/kg.
    !
    !    Output, real(8) SI, the entropy, in KJ/(kg degrees Kelvin).
    !
    !    Output, real(8) UI, the internal energy, in KJ/kg.
    !
    implicit none
    !
    real(8) ai
    real(8), parameter, dimension ( 18 ) :: c = (/ &
         19.730271018D+00,      20.9662681977D+00,     -0.483429455355D+00, &
         6.05743189245D+00,    22.56023885D+00,       -9.87532442D+00, &
         -4.3135538513D+00,      0.458155781D+00,      -0.047754901883D+00, &
         0.0041238460633D+00,  -0.00027929052852D+00,  0.14481695261D-04, &
         -0.56473658748D-06,     0.16200446D-07,       -0.3303822796D-09, &
         0.451916067368D-11,   -0.370734122708D-13,    0.137546068238D-15 /)
    real(8) cpi
    real(8) cvi
    !real(8) gascon
    real(8) gi
    real(8) hi
    integer i
    real(8) si
    real(8) t
    !real(8) temp
    real(8) tt
    real(8) ui
    !
    !  Refuse to handle zero or negative temperatures.
    !
    if ( t <= 0.0D+00 ) then
       write ( *, '(a)' ) ' '
       write ( *, '(a)' ) 'IDEAL - Fatal error!'
       write ( *, '(a)' ) '  The input temperature T must be positive.'
       write ( *, '(a,g14.6)' ) '  Input value was T = ', t
       stop
    end if

    tt = t / 100.0D+00

    gi = - ( c(1) / tt + c(2) ) * log ( tt )
    do i = 3, 18
       gi = gi - c(i) * tt**(i-6)
    end do

    hi = c(2) + c(1) * ( 1.0D+00 - log ( tt ) ) / tt
    do i = 3, 18
       hi = hi + dble ( i - 6 ) * c(i) * tt**(i-6)
    end do

    cpi = c(2) - c(1) / tt
    do i = 3, 18
       cpi = cpi + dble ( ( i - 6 ) * ( i - 5 ) ) * c(i) * tt**(i-6)
    end do

    ai = gi - 1.0D+00
    ui = hi - 1.0D+00
    cvi = cpi - 1.0D+00
    si = hi - gi
    !
    !  Assign dimensions.
    !
    ai =  R_H2O * t * ai
    cpi = R_H2O     * cpi
    cvi = R_H2O     * cvi
    gi =  R_H2O * t * gi
    hi =  R_H2O * t * hi
    si =  R_H2O     * si
    ui =  R_H2O * t * ui

    return
  end subroutine ideal

  ! ==================================================================================

  subroutine resid ( t, rho, ar, cvr, dpdrr, dpdtr, gr, hr, pr, sr, ur )
    !
    !*******************************************************************************
    !
    !! RESID calculates residual contributions to thermodynamic quantities.
    !
    !
    !  Discussion:
    !
    !    The residual function consists of 40 terms.  The first 36 are
    !    used in a global least squares fit to experimental data.
    !
    !    Three terms were added that contribute only in the immediate
    !    neighborhood of the critical point 
    !      (tk-5) <= T <= (tk+5) C
    !      0.20   <= rho <= 0.44 g/cm3, 
    !
    !    A single term was added for the region of high pressure and 
    !    low temperature: T < 75 C, P > 300 MPa.
    !
    !    Except in these limited regions, the residual function is
    !    given by the first 36 terms.  The equation is
    !   
    !      A(residual)(rho,T)=
    !        sum(i=1 to 36) (g(i)/k(i)) * (T0/T)**(l(i)) (1-exp(-rho))**(k(i))
    !      + sum(i=37 to 40) g(i)*delta(i)**(k(i))
    !        * exp(-alpha(i)*delta(i)**(k(i)) - beta(i)*tau(i)**2)
    !                                                     (Equation 5)
    !   
    !    where
    !
    !      g(i) are coefficients determined by fits to data,
    !      delta(i) are reduced densities (delta(i)=((rho-rho(i))/rho(i))
    !      tau(i) are reduced temperatures (tau(i)=((T-tau(i))/tau(i))
    !      rho(i) are specified densities.
    !      tau(i) are specified temperatures.
    !      The k(i) and l(i) are specified integers.
    !   
    !  Modified:
    !
    !    22 November 1998
    !
    !  Reference:
    !
    !    Lester Haar, John Gallagher and George Kell,
    !    NBS/NRC Steam Tables:
    !    Thermodynamic and Transport Properties and Computer Programs
    !      for Vapor and Liquid States of Water in SI Units,
    !    Hemisphere Publishing Corporation, Washington, 1984,
    !    TJ270.H3
    !
    !    C A Meyer, R B McClintock, G J Silvestri, R C Spencer,
    !    ASME Steam Tables: Thermodynamic and Transport Properties of Steam,
    !    American Society of Mechanical Engineers, 1967.
    !
    !  Parameters:
    !
    !    Input, real(8) T, the temperature, in degrees Kelvin.
    !
    !    Input, real(8) RHO, the density, in G/CM3.
    !
    !    Output, real(8) AR, the residual contribution to the 
    !    Helmholtz function, in KJ/kg.
    !
    !    Output, real(8) CVR, the residual contribution to the 
    !    isochoric (constant volume) heat capacity, in KJ/(kg degrees Kelvin).
    !
    !    Output, real(8) DPDRR, the residual contribution to 
    !    the partial derivative dP(T,RHO)/dRHO, with T held fixed, in 
    !    (MegaPascals CM3)/G.
    !
    !    Output, real(8) DPDTR, the residual contribution to 
    !    the partial derivative dP(T,RHO)/dT, with RHO held fixed, 
    !    in MegaPascals/degrees Kelvin.
    !
    !    Output, real(8) GR, the residual contribution to the Gibbs 
    !    function, in KJ/kg.
    !
    !    Output, real(8) HR, the residual contribution to the 
    !    enthalpy, in KJ/kg.
    !
    !    Output, real(8) PR, the residual contribution to the pressure, 
    !    in MegaPascals.
    !
    !    Output, real(8) SR, the residual contribution to the entropy, 
    !    in KJ/(kg degrees Kelvin).
    !
    !    Output, real(8) UR, the residual contribution to the 
    !    internal energy, in KJ/kg.
    !
    implicit none
    !
    real(8), parameter, dimension ( 4 ) :: aad = (/ &
         34.0D+00, 40.0D+00, 30.0D+00, 1050.0D+00 /)
    real(8), parameter, dimension ( 4 ) :: aat = (/ &
         20000.0D+00, 20000.0D+00, 40000.0D+00, 25.0D+00 /)
    real(8), parameter, dimension ( 4 ) :: adz = (/ &
         0.319D+00, 0.319D+00, 0.319D+00, 1.55D+00 /)
    real(8) ar
    real(8) att
    real(8), parameter, dimension ( 4 ) ::  atz = (/ &
         640.0D+00, 640.0D+00, 641.6D+00, 270.0D+00 /)
    real(8) cvr
    real(8) dadt
    real(8) ddz
    real(8) del
    real(8) dex
    real(8) dfdt
    real(8) dpdrr
    real(8) dpdtr
    real(8) e
    real(8) errtol
    real(8) ex0
    real(8) ex1
    real(8) ex2
    real(8) fct
    real(8), parameter, dimension ( 40 ) :: g = (/ &
         -530.62968529023D+00,  0.22744901424408D+04, 0.78779333020687D+03, &
         -69.830527374994D+00,  0.17863832875422D+05,-0.39514731563338D+05, &
         0.33803884280753D+05, -0.13855050202703D+05,-0.25637436613260D+06, &
         0.48212575981415D+06, -0.34183016969660D+06, 0.12223156417448D+06, &
         0.11797433655832D+07, -0.21734810110373D+07, 0.10829952168620D+07, &
         -0.25441998064049D+06, -0.31377774947767D+07, 0.52911910757704D+07, &
         -0.13802577177877D+07, -0.25109914369001D+06, 0.46561826115608D+07, &
         -0.72752773275387D+07,  0.41774246148294D+06, 0.14016358244614D+07, &
         -0.31555231392127D+07,  0.47929666384584D+07, 0.40912664781209D+06, &
         -0.13626369388386D+07,  0.69625220862664D+06,-0.10834900096447D+07, &
         -0.22722827401688D+06,  0.38365486000660D+06, 0.68833257944332D+04, &
         0.21757245522644D+05, -0.26627944829770D+04,-0.70730418082074D+05, &
         -0.225D+00, -1.68D+00, 0.055D+00, -93.0D+00 /)
    !real(8) gascon
    real(8) gr
    real(8) hr
    integer i
    integer, parameter, dimension ( 40 ) :: ii = (/ &
         0,0,0,0,1,1,1,1,2,2,2,2,3,3,3,3,4,4,4,4,5,5,5,5,6,6,6,6, &
         8,8,8,8,2,2,0,4,2,2,2,4 /)
    integer j
    integer, parameter, dimension ( 40 ) :: jj = (/ &
         2,3,5,7,2,3,5,7,2,3,5,7,2,3,5,7,2,3,5,7,2,3,5,7,2,3,5,7,&
         2,3,5,7,1,4,4,4,0,2,0,0 /)
    integer k
    integer l
    integer nc
    real(8) pr
    real(8) q10
    real(8) q20
    real(8) q2a
    real(8) q5t
    real(8) qm
    real(8) qp
    real(8) qr(11)
    real(8) qt(10)
    real(8) rho
    real(8) sr
    real(8), parameter :: s_ref = 7.6180720166752D+00
    real(8) t
    real(8), parameter :: t_ref = 647.073D+00
    real(8) tau
    real(8) tx
    real(8), parameter :: u_ref = - 4328.4549774261D+00
    real(8) ur
    real(8) v
    !
    errtol = sqrt ( epsilon ( errtol ) )
    !
    !  Refuse to handle zero or negative temperatures.
    !
    if ( t <= 0.0D+00 ) then
       write ( *, '(a)' ) ' '
       write ( *, '(a)' ) 'RESID - Fatal error!'
       write ( *, '(a)' ) '  The input temperature T must be positive.'
       write ( *, '(a,g14.6)' ) '  Input value was T = ', t
       stop
    end if
    !
    !  Refuse to handle zero or negative density.
    !
    if ( rho <= 0.0D+00 ) then
       write ( *, '(a)' ) ' '
       write ( *, '(a)' ) 'RESID - Fatal error!'
       write ( *, '(a)' ) '  The input density RHO must be positive.'
       write ( *, '(a,g14.6)' ) '  Input value was RHO = ', rho
       stop
    end if

    nc = 36
    dpdrr = 0.0D+00
    pr = 0.0D+00
    ar = 0.0D+00
    dadt = 0.0D+00
    cvr = 0.0D+00
    dpdtr = 0.0D+00

    ex0 = - rho
    ! ex0 = max ( ex0, - 225.0D+00 )
    ! ex0 = min ( ex0, 225.0D+00 )
    e = exp ( ex0 )

    q10 = rho * rho * e
    q20 = 1.0D+00 - e

    qr(1) = 0.0D+00
    qr(2) = q10
    do i = 2, 10
       qr(i+1) = qr(i) * q20
    end do

    v = t_ref / t
    qt(1) = t / t_ref
    do i = 2, 10
       qt(i) = qt(i-1) * v
    end do

    do i = 1, nc

       k = ii(i) + 1
       l = jj(i)
       qp = g(i) * qr(k+1) * qt(l+1)
       pr = pr + qp

       dpdrr = dpdrr + ( 2.0D+00 / rho - ( 1.0D+00 - e * dble ( k - 1 ) / &
            ( 1.0D+00 - e ) ) ) * qp

       ar = ar + g(i) * qr(k+2) * qt(l+1) / ( rho**2 * e * dble ( k ) &
            * R_H2O * t )

       dfdt = ( 1.0D+00 - e )**k * dble ( 1 - l ) * qt(l+2) / t_ref / dble ( k )

       dadt = dadt + g(i) * dfdt

       dpdtr = dpdtr + g(i) * dfdt * rho**2 * e * dble ( k ) / ( 1.0D+00 - e )

       cvr = cvr + g(i) * dble ( l ) * dfdt / R_H2O

    end do

    qp = 0.0D+00
    q2a = 0.0D+00

    do j = 37, 40

       k = ii(j)
       ddz = adz(j-36)
       del = rho / ddz - 1.0D+00

       if ( abs ( del ) < errtol ) then
          del = errtol
       end if

       ex1 = - aad(j-36) * del**k
       !   ex1 = max ( ex1, - 225.0D+00 )
       !   ex1 = min ( ex1, 225.0D+00 )
       dex = exp ( ex1 ) * del**jj(j)

       att = aat(j-36)
       tx = atz(j-36)
       tau = ( t / tx ) - 1.0D+00

       ex2 = - att * tau**2
       !   ex2 = max ( ex2, - 225.0D+00 )
       !   ex2 = min ( ex2, 225.0D+00 )
       q10 = dex * exp ( ex2 )

       qm = dble ( jj(j) ) / del - dble ( k ) * aad(j-36) * del**(k-1)
       fct = qm * rho**2 * q10 / ddz

       q5t = fct * ( 2.0D+00 / rho + qm / ddz ) - ( rho / ddz )**2 * q10 * &
            ( dble ( jj(j) ) / del**2 + dble ( k * ( k - 1 ) ) * aad(j-36) * &
            del**(k-2) )

       dpdrr = dpdrr + q5t * g(j)
       qp = qp + g(j) * fct
       dadt = dadt - 2.0D+00 * g(j) * att * tau * q10 / tx
       dpdtr = dpdtr - 2.0D+00 * g(j) * att * tau * fct / tx

       q2a = q2a + t * g(j) * att * ( 4.0D+00 * ex2 + 2.0D+00 ) * q10 / tx**2

       ar = ar + q10 * g(j) / ( R_H2O * t )

    end do

    cvr = cvr + q2a / R_H2O
    pr = pr + qp
    sr = - dadt / R_H2O
    ur = ar + sr
    !
    !  Assign dimensions.
    !
    ar =  R_H2O * t *  ar
    cvr = R_H2O *     cvr
    sr =  R_H2O *      sr
    ur =  R_H2O * t *  ur
    !
    !  Adjust energies.
    !
    ar = ar + R_H2O * t * s_ref - R_H2O * u_ref
    sr = sr - R_H2O * s_ref
    ur = ur - R_H2O * u_ref

    gr = ar + pr / rho
    hr = ur + pr / rho

    return
  end subroutine resid

  ! ==================================================================================
  
  subroutine tdpsdt ( t, dp )
    !
    !*******************************************************************************
    !
    !! TDPSDT computes the quantity T * dP(Sat)/dT.
    !
    !
    !  Discussion:
    !
    !    Here T is the temperature and P(Sat) is the vapor pressure.  
    !    It is used by TSAT_EST and TSAT.
    !
    !  Reference:
    !
    !    Lester Haar, John Gallagher and George Kell,
    !    NBS/NRC Steam Tables:
    !    Thermodynamic and Transport Properties and Computer Programs
    !      for Vapor and Liquid States of Water in SI Units,
    !    Hemisphere Publishing Corporation, Washington, 1984,
    !    TJ270.H3
    !
    !    C A Meyer, R B McClintock, G J Silvestri, R C Spencer,
    !    ASME Steam Tables: Thermodynamic and Transport Properties of Steam,
    !    American Society of Mechanical Engineers, 1967.
    !
    !  Parameters:
    !
    !    Input, real(8) T, the temperature, in degrees Kelvin.
    !
    !    Output, real(8) DP, the value T*(dP(Sat)/dT), 
    !    in Pa.
    !
    implicit none
    !
    real(8), parameter, dimension ( 8 ) :: a = (/ &
         -7.8889166D+00,   2.5514255D+00,   -6.716169D+00, 33.239495D+00, &
         -105.38479D+00,   174.35319D+00,   -148.39348D+00,  48.631602D+00 /)
    real(8) b
    real(8) c
    real(8) dp
    integer i
    real(8) q
    real(8) t
    real(8), parameter :: t_ref = 647.25D+00
    real(8) v
    real(8) w
    real(8) y
    real(8) z
    !
    !  Refuse to handle zero or negative temperatures.
    !
    if ( t <= 0.0D+00 ) then
       write ( *, '(a)' ) ' '
       write ( *, '(a)' ) 'TDPSDT - Fatal error!'
       write ( *, '(a)' ) '  The input temperature T must be positive.'
       write ( *, '(a,g14.6)' ) '  Input value was T = ', t
       stop
    end if

    v = t / t_ref
    w = 1.0D+00 - v
    b = 0.0D+00
    c = 0.0D+00
    do i = 1, 8
       z = dble ( i + 1 ) / 2.0D+00
       y = a(i) * w**z
       c = c + ( y / w ) * ( 0.5D+00 - 0.5D+00 * dble ( i ) - 1.0D+00 / v )
       b = b + y
    end do

    q = b / v
    dp = 22.093D+00 * exp ( q ) * c

    dp = dp*1.0d6 ! convert MPa to Pa

    return
  end subroutine tdpsdt

  ! ==================================================================================
  
  subroutine therm ( t, rho, a, cjth, cjtt, cp, cv, dpdr, dpdt, g, h, p, s, u )
    !
    !*******************************************************************************
    !
    !! THERM calculates thermodynamic functions given temperature and density.
    !
    !
    !  Discussion:
    !
    !    Thermodynamic values were calculated from an analytic equation
    !    that approximates the Helmholtz function (specific Helmholtz
    !    energy) for ordinary water and steam, of the form A=A(rho,T)
    !    where A is the Helmholtz function, rho the density, and T
    !    the absolute (thermodynamic) temperature.  Any thermodynamic
    !    value for any state, liquid, vapor or metastable, may be
    !    calculated by differentiation of this equation in accord with
    !    the first and second laws of thermodynamics.
    ! 
    !    The International Association for the Properties of Steam
    !    has provisionally accepted this formulation for the range
    !    273.15 <= T <= 1273.15 degrees Kelvin, where, for 423.15 <= T,
    !    the maximum pressure is Pmax = 1500 MPa = 15000 bar, and for
    !    273.15 <= T < 423.15, the maximum pressure is
    !    Pmax = 100 * (5 + (T-273.15)/15) MPa.
    ! 
    !    Close to the critical point, a small region is excluded:
    !    Abs(T-Tk) < 1, abs((rho-rhok)/rhok) < 0.3.
    ! 
    !    The equation has a wider useful range, namely, fluid states
    !    of pure, undissociated water and steam defined by
    !    260 <= T <= 2500 K and 0 <= P <= 3000 MPa.
    ! 
    !    Thermodynamic property values for specific volume, density,
    !    specific internal energy, specific enthalpy, and specific
    !    entropy of water and steam were tabulated over the range
    !    0 <= t <= 2000 C, 0 <= P <= 3000 MPa.  The reference
    !    state is the liquid at the triple point, for which the
    !    internal energy and entropy have been assigned the value zero.
    !     
    !    Thermodynamic quantities are determined from the Helmholtz function
    !    A(rho,T), which is computed as the sum of three terms:
    !
    !      A(rho,T) = A(base)(rho,T) + A(residual)(rho,T) + A(ideal)(T)
    !                                                       (Equation 1)
    !
    !    Because A(rho,T) is everywhere single valued and analytic,
    !    we can derive closed form relations for all other properties.
    !    In the following, unless otherwise indicated, the independent
    !    variables are temperature T and density RHO, and differentiation
    !    with respect to one variable is to imply that the other is fixed.
    ! 
    !    Pressure:                  P       = RHO**2 * dA/dRHO
    !    Density derivative:        dP/dRHO = 2*P/RHO + RHO**2 * d2A/dRHO2
    !    Temperature derivative:    dP/dT   = RHO**2 * d2A/(dRHO dT)
    !    Specific entropy:          S       = - dA/dT
    !    Specific internal energy:  U       = A + T*S
    !    Specific enthalpy:         H       = U + P/RHO
    !    Specific heat capacity
    !      at constant volume:      Cv      = - T * d2A/dT2
    !    Specific Gibbs function:   G       = A + P/RHO
    !    Specific heat capacity
    !      at constant pressure:    Cp      = Cv + (T*(dP/dT)**2)/(RHO**2*dP/dRHO)
    !    Speed of sound:            Omega   = Sqrt ((Cp/Cv) * dP/dRHO)
    !    Second virial coefficient: B       = 1/(2*R*T) * (d2P/dRHO2) (at RHO=0)
    !    Isothermal Joule-Thomson
    !      coefficient:             DeltaT  = (dH/dP) (fixed T) =
    !                                         (1/RHO)-(T*dP/dT)/(RHO**2*dP/dRHO)
    !    Joule-Thomson coefficient: Mu      = (dT/dP) (fixed H) = DeltaT/Cp
    !    Isentropic temperature-
    !      pressure coefficient:    BetaS   = (dT/dP) (fixed S) =
    !                                         (DeltaT - 1/RHO)/Cp
    !   
    !  Modified:
    !
    !    19 November 1998
    !
    !  Reference:
    !
    !    Lester Haar, John Gallagher and George Kell,
    !    NBS/NRC Steam Tables:
    !    Thermodynamic and Transport Properties and Computer Programs
    !      for Vapor and Liquid States of Water in SI Units,
    !    Hemisphere Publishing Corporation, Washington, 1984,
    !    TJ270.H3
    !
    !    C A Meyer, R B McClintock, G J Silvestri, R C Spencer,
    !    ASME Steam Tables: Thermodynamic and Transport Properties of Steam,
    !    American Society of Mechanical Engineers, 1967.
    !
    !  Parameters:
    !
    !    Input, real(8) T, the temperature, in degrees Kelvin.
    !
    !    Input, real(8) RHO, the fluid density, in G/CM3.
    !
    !    Output, real(8) A, the Helmholtz function, in KJ/kg.
    !
    !    Output, real(8) CJTH, the Joule-Thomson coefficient,
    !    in K/MegaPascals.
    !
    !    Output, real(8) CJTT, the isothermal Joule-Thomson coefficient,
    !    in CM3/G.
    !
    !    Output, real(8) CP, the isobaric (constant pressure) heat
    !    capacity, in KJ/(kg degrees Kelvin).
    !
    !    Output, real(8) CV, the isochoric (constant volume) heat capacity,
    !    in KJ/(kg degrees Kelvin).
    !
    !    Output, real(8) DPDR, the partial derivative 
    !    dP(T,RHO)/dRHO, with T held fixed, in MegaPascals*CM3/G.
    !
    !    Output, real(8) DPDT, the partial derivative 
    !    dP(T,RHO)/dT, with RHO held fixed, in MegaPascals/degrees Kelvin.
    !
    !    Output, real(8) G, the Gibbs free energy, in KJ/kg.
    !
    !    Output, real(8) H, the enthalpy, in KJ/kg.
    !
    !    Output, real(8) P, the pressure, in Pa.
    !
    !    Output, real(8) S, the entropy, in KJ/(kg degrees Kelvin).
    !
    !    Output, real(8) U, the internal energy, in KJ/kg.
    !
    implicit none
    !
    real(8) a
    real(8) ab
    real(8) ai
    real(8) ar
    real(8) cjth
    real(8) cjtt
    real(8) cp
    real(8) cpi
    real(8) cv
    real(8) cvb
    real(8) cvi
    real(8) cvr
    logical, parameter :: debug = .false.
    !  logical, parameter :: debug = .true.
    real(8) dpdr
    real(8) dpdrb
    real(8) dpdrr
    real(8) dpdt
    real(8) dpdtb
    real(8) dpdtr
    real(8) g
    real(8) gb
    real(8) gi
    real(8) gr
    real(8) h
    real(8) hb
    real(8) hi
    real(8) hr
    real(8) p
    real(8) pb
    real(8) pr
    real(8) rho
    real(8) s
    real(8) sb
    real(8) si
    real(8) sr
    real(8) t
    real(8) u
    real(8) ub
    real(8) ui
    real(8) ur
    !
    !  Refuse to handle zero or negative temperatures.
    !
    if ( t <= 0.0D+00 ) then
       write ( *, '(a)' ) ' '
       write ( *, '(a)' ) 'THERM - Fatal error!'
       write ( *, '(a)' ) '  The input temperature T must be positive.'
       write ( *, '(a,g14.6)' ) '  Input value was T = ', t
       stop
    end if
    !
    !  Refuse to handle zero or negative density.
    !
    if ( rho <= 0.0D+00 ) then
       write ( *, '(a)' ) ' '
       write ( *, '(a)' ) 'THERM - Fatal error!'
       write ( *, '(a)' ) '  The input density RHO must be positive.'
       write ( *, '(a,g14.6)' ) '  Input value was RHO = ', rho
       stop
    end if

    call ideal ( t, ai, cpi, cvi, gi, hi, si, ui )

    call resid ( t, rho, ar, cvr, dpdrr, dpdtr, gr, hr, pr, sr, ur )

    call base ( t, rho, ab, cvb, dpdrb, dpdtb, gb, hb, pb, sb, ub )

    a =       ab +    ar +  ai
    cv =     cvb +   cvr + cvi

    if ( debug ) then
       write ( *, * ) ' '
       write ( *, * ) 'THERM:'
       write ( *, * ) '  CVB = ', cvb
       write ( *, * ) '  CVR = ', cvr
       write ( *, * ) '  CVI = ', cvi
       write ( *, * ) '  CV  = ', cv
    end if


    dpdr = dpdrb + dpdrr
    dpdt = dpdtb + dpdtr
    p =       pb +    pr
    s =       sb +    sr +  si
    u =       ub +    ur +  ui

    if ( debug ) then
       write ( *, * ) ' '
       write ( *, * ) 'THERM:'
       write ( *, * ) '  UB = ', ub
       write ( *, * ) '  UR = ', ur
       write ( *, * ) '  UI = ', ui
    end if

    g = a + p / rho
    h = u + p / rho
    cp = cv + t * dpdt**2 / ( dpdr * rho**2 )
    cjtt = 1.0D+00 / rho - t * dpdt / ( dpdr * rho**2 )
    cjth = - cjtt / cp

    p = 1.0d6*p ! convert pressure to Pa

    return
  end subroutine therm

  ! ==================================================================================
  
end module thermodynamics

